s" lib.fs" included ...

# Day 1, Part 1

Read the problem at:
[https://adventofcode.com/2021/day/1](https://adventofcode.com/2021/day/1)

We are given an ordered set of numbers, and asked to count the number
of times when the two consecutive elements are ordered such that the
next one is greater than the previous.

First, create an array of 2000 elements. We will use the word `array`
defined in `lib.fs` (located in this repository) to simplify working
with arrays.

```forth
2000 cell array data
```
> The word `array` creates a word with the given name, in this case
> `data`, which when executed takes an offset from the stack and
> leaves the address of the element. `array` itself takes the number
> of elements, and the size of each element on the stack to create the
> array word, whose execution behaviour I just described.
> 
> Note, `array` is what's called a "defining word" in Forth. Defining
> words parse the next word from input buffer, and create a word with
> that name in the dictionary. Their execution semantics are defined
> in after `does>` in the definition of the defining word.

The new array word `data` can be used to index into an element. For
example, `0 data` will leave the address of the 0th (first) element in
the array.

Secondly, we need to load the data into memory. For this I use a word
called `array-load`. It takes the file name to execute and the address
of first element. The provided file is formatted in a way to load the
numbers into the dictionary.

```forth
s" 1.txt" 0 data array-load
```

Next, define `depth<` that will take the address of an element in the
array, and compare it to the previous element.

```forth
: depth< ( addr -- f ) cell - @   over @  < ;
```

Next, define `collect` that will take an element address and the
accumulator, and add 1 to the accumulator if `depth<` is true.

```forth
: collect ( addr acc -- addr acc ) swap dup depth< 0= 1+ rot + ;
```

`0=` and `1+` are two idiomatic phrases in Forth. `0=` has the effect
of taking the one's complement, thus inverting a boolean flag. `1+`
after a boolean flag turns it into `0` for true, and `1` for false. I
had seen this trick in Dr. C.H.Ting's Zen and Forth book.

Finally, define `solve` which will use the word `reduce` to accumulate
the result from left to right starting from the second element.

It starts from the second element because `depth<` always compares
with the previous element.

> Note: `reduce` is a custom word defined in `lib.fs`. It takes an
> [execution
> token](https://www.complang.tuwien.ac.at/forth/gforth/Docs-html/Execution-Tokens-Tutorial.html),
> the address of a counted array, and the initial value of the
> accumulator as parameter.

```forth
: day1-part1    ['] collect 1 data 1999 0 reduce ;
```

Run and assert the result.
```forth
day1-part1 1548 assert
```

[Next part](./1-2.fs.md)

