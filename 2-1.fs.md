s" lib.fs" included ...

# Day 2, Part 1

Read the problem at:
[https://adventofcode.com/2021/day/2](https://adventofcode.com/2021/day/2)

Day 2 is particularly simple with Forth, and can be solved only using
the standard word set. It's because the provided input text is valid
Forth source.

Snippet from the input:

```ignore
forward 5
down 5
forward 8
up 3
down 8
forward 2
```

First define the `position` and `depth` variables:

```forth
variable position   variable depth
```

Now we can define the commands `forward`, `down` and `up` to parse the
next number, and do the asked calculation for each command.

```forth
: forward ( n -- ) parse-name evaluate position +! ;
: down    ( n -- ) parse-name evaluate depth +! ;
: up      ( n -- ) parse-name evaluate depth @ swap - depth ! ;
```

Finally, define `solve` to include the text file, which will execute
as its being parsed by the Forth compiler/interpreter on the
fly. After it's done, multiply `position` and `depth`.

```forth
: day2-part1 s" 2.txt" included position @ depth @ * ;
```

Run and assert the result.
```forth
day2-part1 2150351 assert
```

[Next part](./2-2.fs.md)
