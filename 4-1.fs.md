s" lib.fs" included ...

# Day 4, Part 1

Read the problem at:
[https://adventofcode.com/2021/day/4](https://adventofcode.com/2021/day/4)

Today we make the computer play bingo by itself with lots and lots of
bingo boards.

Sample input data looks like this:

```
7 4 9 5 11 17 23 2 0 14 21 24 10 16 13 6 15 25 12 22 18 20 8 19 3 26 1

22 13 17 11  0
 8  2 23  4 24
21  9 14 16  7
 6 10  3 18  5
 1 12 20 15 19

 3 15  0  2 22
 9 18 13 17  5
19  8  7 25 23
20 11 10 24  4
14 21 16 12  6

14 21 17 24  4
10 16 15  9 19
18  8 23 26 20
22 11 13  6  5
 2  0 12  3  7
```

The first line is the random number sequence. There are three 5x5
bingo boards. Each round a number is pulled, and everyone marks the
number on their board. The board that completes a horizontal or
vertical line first calls: "BINGO!".

## Constants and loading input

```forth
5       constant #N     ( dimension of board )
#N #N * constant #M

s" 4.txt" open-file
```

## Parsing words

```forth
( parsing words )
: parse-line      ( -- ) 1 parse-matrix ;
: parse-randoms   ( -- ) parse-line 0 , ;
: skip-line       ( -- ) read-line drop ;
: parse-boards    ( -- ) parse-matrix-all ;
: #size    ( addr -- n ) here swap - cell / ;
: #randoms ( addr -- n ) #size ;
: #boards  ( addr -- n ) #size #M / ;

create randoms parse-randoms randoms #randoms constant #randoms
skip-line
create boards  parse-boards  boards  #boards  constant #boards
```

## Random number words

```forth
( random number words )
variable #rnd randoms #rnd !

: last   ( -- n ) randoms #randoms cells + @ ;
: next++   ( -- ) cell #rnd +! ;
: @rnd   ( -- n ) #rnd @ @ ;
: next ( -- n f ) @rnd next++ dup last <> ;
```

## Board words

```forth
( board words )
hex 8000 decimal constant #found  ( found mask )

: board     ( n -- addr ) #M cells * boards + ;
: mark    ( addr -- dup ) dup @ #found or swap ! ;
: square    ( addr -- n ) @ #found invert and ;
: cell++ ( addr -- addr ) cell + ;
: same?   ( n addr -- f ) over over square = ;
```

## Number marking words

```forth
( number marking words )
: mark ( n addr -- )
  #M 0 do
    same? if dup mark then cell++
  loop 2drop
;
: mark  ( n u -- ) board mark ;
: mark    ( n -- ) #boards 0 do dup i mark loop drop ;
: marked? ( n -- ) #found and 0> ;

: square-x ( addr n -- n ) cells + @ ;
: square-y ( addr n -- n ) #N * cells + @ ;
```

# Bingo checking words

```forth
( bingo checking words )
defer square-at

: bingo-dir? ( addr -- f )
  true #N 0 do over i square-at marked? and loop nip ;

: bingo-x? ( addr -- f ) ['] square-x is square-at bingo-dir? ;
: bingo-y? ( addr -- f ) ['] square-y is square-at bingo-dir? ;

defer bingo-at?

: bingo-row? ( addr n -- f ) #N * cells + bingo-x? ;
: bingo-col? ( addr n -- f ) cells + bingo-y? ;

: bingo? ( addr -- f )
  #N 0 do
    dup i bingo-at? if drop true unloop exit then
  loop drop false
;

: bingo-x? ( addr -- f) ['] bingo-row? is bingo-at? bingo? ;
: bingo-y? ( addr -- f) ['] bingo-col? is bingo-at? bingo? ;

: bingo? ( addr -- f )
  false over bingo-x? or swap bingo-y? or ;

: bingo? ( -- n )
  #boards 0 do i board bingo? if i unloop exit then loop -1 ;
```

## Game loop and score words

```forth
( game loop and score words )
variable number
: save-number ( n -- n ) dup number ! ;

: play ( -- n )
  begin
    next
  while
    save-number
    mark bingo? dup true <> if exit then drop
  repeat drop 0
;

: score ( n -- n )
  board 0
  #M 0 do
    over i cells + @ marked? 0= if
      over i cells + @ +
    then
  loop nip
;
: score ( n -- n ) score number @ * ;

```

## Solution

```forth
: day4-part1 ( -- n ) play score ;

day4-part1 8136 assert

```

## Forth style code conventions

Some Forth style conventions I have used:

- Build a vocabulary for each isolated sub problem.

When the problem is decomposed into smaller sub problems, each word
are defined in terms of the previous words specific and local to that
sub problem.

- Create as many small words as possible.

This allows every new word to be defined idealy on a single line, just
like in a language dictionary book. It is simpler to consider small
words.

- "Extend" the meaning of a word by re-defining it.

This is not a popular practice, and is often unnecessary since the
whole word can be written on multiple lines. But I have benefited from
this in particular thanks to the stack effect descriptions, and
ability to test each sub word in isolation.

Imaginary example to demonstrate it:

```
: orange round fruit ;
: orange 42 pore-density 53 vitamin-c-density orange ;
: orange 12 peel-thickness orange ;
```

In the beginning the word `orange` only means a `round fruit`. But
later we extend its meaning to cover its `pore-density`,
`vitamin-c-density` and `peel-thickness`. In the end only the last
meaning of the word `orange` is visible to the program that follows.

- Define the words in a way to reduce "stack juggling".

With attention and care the stack words such as `rot`, `nip`, `swap`,
`over` can be eliminated. `pick`, `roll` is never needed. A word
should never need more than 2 elements on stack. If you need more,
create smaller words. This increases readability and testability
greatly.

- Test as you build it.

It is not included in the final code, but after each word set
definition list, it is possible to include tests (which won't be
included into the compiled code).

## Extra bonus

Some extra bonus for visualization:

```
: .2  ( d -- ) 0 <#       32 hold # #       32 hold 32 hold #> type ;
: .2* ( d -- ) 0 <# [char] ) hold # # [char] ( hold 32 hold #> type ;

: $board   ( n -- ) cr cr ." Board: " dup . ;
: new-line ( n -- ) #N mod 0= if cr then ;

: marked?  ( addr -- )
  @ marked? if   dup square .2*
            else dup @ .2 then ;

: print-board ( n -- )
  $board board #M 0 do i new-line dup marked? cell++ loop ;

: print-boards ( -- ) #boards 0 do i print-board drop loop ;
```

It displays:

```
Board: 0
  22   13  (17) (11) (00)
  08  (02) (23) (04) (24)
 (21) (09) (14)  16  (07)
  06   10   03   18  (05)
  01   12   20   15   19

Board: 1
  03   15  (00) (02)  22
 (09)  18   13  (17) (05)
  19   08  (07)  25  (23)
  20  (11)  10  (24) (04)
 (14) (21)  16   12   06

Board: 2
 (14) (21) (17) (24) (04)
  10   16   15  (09)  19
  18   08  (23)  26   20
  22  (11)  13   06  (05)
 (02) (00)  12   03  (07)
```

[Next part](./4-2.fs.md)
