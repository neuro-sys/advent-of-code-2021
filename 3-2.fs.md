s" lib.fs" included ...

# Day 3, Part 2

As Part 2, as usual, has some parts the same as Part 1, include the
Part 1 code here with some additions and modifications:

```forth
1000 constant #data

#data cell array data
s" 3.txt" 0 data array-load

variable (#num) #data (#num) !

: #num (#num) @ ;

12   constant #bit
4095 constant #mask

create ones #bit cells allot

( bit count vocabulary )
: add       ( 1|0 n -- ) cells ones + +! ;
: bit  ( addr n -- 1|0 ) swap @ swap rshift 1 and ;
: scan       ( addr -- ) dup #bit 0 do dup i bit i add loop drop ;
: reset-ones      ( -- ) #bit 0 do 0 ones i cells + ! loop ;
: count-ones ( addr -- ) reset-ones ['] scan swap #num for-each ;

( gamma and epsilon vocabulary )
: ones      ( n -- n ) cells ones + @ ;
: zeroes    ( n -- n ) ones #num swap - ;
: common?   ( n -- f ) 2 * #num > ;
: equal?    ( n -- f ) 2 * #num = ;
: most    ( n -- 0|1 ) ones common? 0= 1+ ;
: least   ( n -- 0|1 ) ones common? 1+ ;

: gamma-bit ( n -- 0|1 )
   dup ones equal? if drop 1 else most then ;

: epsilon-bit ( n -- 0|1 )
   dup ones equal? if drop 0 else least then ;
```

Now we have a bunch of words to say things like:

* `5 ones` to mean the number of ones in 5th bit,
* `2 zeroes common?` to ask if the 2nd bits are more zeroes than ones,
* `2 most` to mean the most common bit of 2nd bits.

## Bit criteria

There are two bit criteria, `oxygen-rating` and `co2-rating`. We want
to define these words to give us the respective values.

`oxygen-rating` is found by filtering the list to include only the
ones where the bit for a given index matches the `gamma-bit`.

`gamma-rating` is the same, except it should match `epsilon-bit`
instead.

We already have the words to find the `gamma-bit` and `epsilon-bit`
for any given bit index over the data set.

```raw
3 gamma-bit .
```

This would print the 3rd `gamma-bit` of the `data` set.

Now we can define `gamma-bit?` and `epsilon-bit?` to answer whether a
given element and its given bit is the correct one.

```forth
: gamma-bit?   ( addr u -- f ) 2dup bit rot drop swap gamma-bit = ;
: epsilon-bit? ( addr u -- f ) 2dup bit rot drop swap epsilon-bit = ;
```

Note that `rot drop swap` is a code smell in terms of idiomatic
Forth. Ideally the words should be defined in a way to prevent stack
juggling. Also repetitions can be factored out.

Even without any of that, it could still be written a bit simpler
using the return stack:

```forth
: gamma-bit?   ( addr u -- f ) dup >r bit r> gamma-bit   = ;
: epsilon-bit? ( addr u -- f ) dup >r bit r> epsilon-bit = ;
```

This word can now be used like this:

```
element 3 gamma-bit?
```

This would tell us if the `3`rd bit of the `element` is a `gamma-bit`.

Now that we can tell whether an element's bit is a `gamma-bit` or
`epsilon-bit`, we can write our criteria.

```forth
variable bit-index

: oxygen? ( addr -- addr )
   dup bit-index @ gamma-bit?   if dup @ , then ;

: co2? ( addr -- addr )
   dup bit-index @ epsilon-bit? if dup @ , then ;

```

These criteria save the bit pattern into a new list if their
`bit-index` is a `gamma-bit` for `oxygen`, or `epsilon-bit` for `co2`
ratings respectively.

Now let's build some more vocabulary to find the `oxygen` and `co2`
ratings.

```forth
defer criteria

: last?         ( -- f ) #num 1 = ;
: save-list  ( addr -- ) ['] criteria swap #num for-each 0 , ;
: save-count ( addr -- ) #count (#num) ! ;
: bit-index--     ( -- ) bit-index @ 1- bit-index ! ;
: init-bit-index  ( -- ) #bit 1- bit-index ! ;
```

These words are necessary to build the main loop for filtering the
valid bit patterns. For example `save-list` will save the bit
patterns that match the `criteria`. And, `last?` will ask if there
is only one element left in the current list.

```forth
: rating ( addr -- n )
  init-bit-index
  dup save-count
  #bit 0 do
    last? if unloop exit then
    dup count-ones
    here
      swap save-list 
    dup save-count
    bit-index--
  loop
;
```

With better vocabulary and more factoring, `rating` could have been
written better.

Now by setting `criteria` to either `oxygen?` or `co?`, we can get the
`rating` for each.

```forth
: oxygen-rating ['] oxygen? is criteria 0 data rating @ ;
: co2-rating    [']    co2? is criteria 0 data rating @ ;
```

Calculate the result and assert.

```forth
: day3-part2 oxygen-rating co2-rating * ;

day3-part2 3570354 assert
```

[Next part](./4-1.fs.md)
