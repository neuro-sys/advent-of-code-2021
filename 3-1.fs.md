s" lib.fs" included ...

# Day 3, Part 1

Read the problem at:
[https://adventofcode.com/2021/day/3](https://adventofcode.com/2021/day/3)

This day is about counting the bits in a set of bit patterns in
various ways.

The input file looks like this:

```ignore
011101101110
010110001101
100111000110
011110101000
```

As before, to ease the parsing, I used Forth interpreter's ability to
read in binary numbers, and load them into memory.

First, we set the number of elements in the data set to a constant.

```forth
1000 constant #data
```

Then create an array and load the binary numbers.

```forth
#data cell array data
s" 3.txt" 0 data array-load
```

> Note: `#count` is defined in `lib.fs` and counts the number of cells
> until reaching zero for the provided address, in this case `data`.

Let us define some constants that will be useful:

```forth
12   constant #bit
4095 constant #mask
```

`#bit` is the number of bits in each bit pattern, and `#mask` is used
for bit masking which will be needed later, and is calculated as `2 ^
#bit -1`.

We need to count the number of set bits on each bit index across the
set of bit patterns. For this purpose, let's create an array to hold
the set bit count of each column:

```forth
create ones #bit cells allot
```

We will store the least significant bit count as the first element in
this array.

Now, define `add` to increase the counter of any one bit counter.

```forth
: add ( 1|0 n -- ) cells ones + +! ;
```

Define `bit` to get the bit for the index `n` from the given element
`addr`:

```forth
: bit ( addr n -- 1|0 ) swap @ swap rshift 1 and ;
```

Define `scan` which will scan through all the bits of a given number
and add to the respective bit counter.

```forth
: scan ( addr -- ) dup #bit 0 do dup i bit i add loop drop ;
```

And finally define `count-ones`. It will scan the provided data array
and count the ones for each bit index.

```forth
: count-ones ( addr -- ) ['] scan swap #data for-each ;
```

To demonstrate, `count-ones` would be used as:

```raw
data count-ones
```

At this point, `ones` contain an array of numbers for each bit index,
starting from bit 0, that contains the number of times that bit is set
in the entire set.

The rest of the challenge is asking for the multiplication result of
so-called `gamma` and `epsilon` values derived from these bit counts.

## `gamma`

The most common bit of each bit index across the set is the
`gamma-bit`. All the gamma bits from left to right form the `gamma`
value. Note that left to right implies we start from the most
significant bit first.

## `epsilon`

`epsilon` is simply the one's complement of `gamma`.

## Finding the `gamma`

Define `one-count` to get the bit count for the bit index:
```forth
: one-count ( n -- n )   cells ones + @ ;
```

Define `common?` that answers whether a number is the most common (or
equal):

```forth
: common? ( n -- f )   2 * #data >= ;
```

Define `gamma-bit` that will give the gamma-bit for the given index:

```forth
: gamma-bit ( n -- 0|1 ) one-count common? 0= 1+ ;
```

Define `gamma` that is all the gamma-bits from most significant to the
least.

```forth
: gamma ( -- n ) 0 #bit 0 do i gamma-bit i lshift or loop ;
```

So far we can answer the `gamma` value of a data series, like this:

```raw
data count-ones gamma .
```

`epsilon` will simply take the one's complement of `gamma`:

```forth
: epsilon ( -- n ) gamma invert #mask and ;
```

As the solution was defined as the multiplication of `gamma` and
`epsilon`, we can now write the whole program using the vocabulary we
built.

```forth
: day3-part1 0 data count-ones gamma epsilon * ;

day3-part1 4147524 assert

```

[Next part](./3-2.fs.md)
