s" lib.fs" included ...

# Day 4, Part 2

This is the same as Part 1, but instead of the first winner, we are
asked to find the last winner.

```forth
5       constant #N     ( dimension of board )
#N #N * constant #M

s" 4.txt" open-file

( parsing words )
: parse-line      ( -- ) 1 parse-matrix ;
: parse-randoms   ( -- ) parse-line 0 , ;
: skip-line       ( -- ) read-line drop ;
: parse-boards    ( -- ) parse-matrix-all ;
: #size    ( addr -- n ) here swap - cell / ;
: #randoms ( addr -- n ) #size ;
: #boards  ( addr -- n ) #size #M / ;

create randoms parse-randoms randoms #randoms constant #randoms
skip-line
create boards  parse-boards  boards  #boards  constant #boards

( random number words )
variable #rnd randoms #rnd !

: last   ( -- n ) randoms #randoms cells + @ ;
: next++   ( -- ) cell #rnd +! ;
: @rnd   ( -- n ) #rnd @ @ ;
: next ( -- n f ) @rnd next++ @rnd last <> ;

( board words )
create won-boards #boards cells allot

hex 8000 decimal constant #found  ( found mask )

: board     ( n -- addr ) #M cells * boards + ;
: mark    ( addr -- dup ) dup @ #found or swap ! ;
: value     ( addr -- n ) @ #found invert and ;
: cell++ ( addr -- addr ) cell + ;
: match?  ( n addr -- f ) over over value = ;

: wins   ( n -- ) cells won-boards + true swap ! ;
: won? ( n -- f ) cells won-boards + @ true = ;

( number marking words )
: mark ( n addr -- )
  #M 0 do match? if dup mark then cell++ loop 2drop ;
: mark  ( n u -- ) dup won? 0= if board mark else 2drop then ;
: mark    ( n -- ) #boards 0 do dup i mark loop drop ;
: marked? ( n -- ) #found and 0> ;

: square-x ( addr n -- n ) cells + @ ;
: square-y ( addr n -- n ) #N * cells + @ ;

( bingo checking words )
defer square-at

: bingo? ( addr -- f )
  true #N 0 do over i square-at marked? and loop nip ;

: bingo-x ( -- ) ['] square-x is square-at ;
: bingo-y ( -- ) ['] square-y is square-at ;

defer bingo-at?

: bingo-row? ( addr n -- f ) #N * cells + bingo-x bingo? ;
: bingo-col? ( addr n -- f ) cells + bingo-y bingo? ;

: bingo? ( addr -- f )
  #N 0 do
    dup i bingo-at? if drop true unloop exit then
  loop drop false
;

: bingo-x ( addr -- f) ['] bingo-row? is bingo-at? ;
: bingo-y ( addr -- f) ['] bingo-col? is bingo-at? ;

: bingo? ( addr -- f )
  false over bingo-x bingo? or swap bingo-y bingo? or ;

: board-left ( -- n )
  #boards #boards 0 do i won? if 1- then loop ;

variable bingo-found
: bingo? ( -- n )
  0 bingo-found !
  #boards 0 do
    i won? 0= if
      i board bingo? if i wins i bingo-found ! then
    then
  loop bingo-found @
;

( game loop and score words )
variable last-number
variable last-board

: save-number ( n -- ) last-number ! ;
: save-board  ( n -- ) last-board ! ;
: save      ( n n -- ) save-board save-number ;

: play ( -- )
  begin
    next
  while
    dup
    mark bingo? dup -1 <> if save else 2drop then
    board-left 0= if exit then
  repeat drop
;

: score ( n -- n )
  board 0
  #M 0 do
    over i cells + @ marked? 0= if
      over i cells + @ +
    then
  loop nip
;

: score ( n -- n ) last-number @ last-board @ score * ;

: day4-part2 play score ;

day4-part2 12738 assert

```
